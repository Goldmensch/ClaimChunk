import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'de.undercouch.download' version '4.1.1'
    id 'io.freefair.lombok' version "5.3.3.3"
    id 'java'
}

// Plugin information
group 'com.cjburkey'
version '0.0.23-prev4'
project.ext.liveVersion = '0.0.22'
project.archivesBaseName = 'claimchunk'
project.ext.pluginName = 'ClaimChunk'
project.ext.mainClass = 'com.cjburkey.claimchunk.ClaimChunk'

generateLombokConfig {
    enabled = false
}

// Only used if you run `gradlew installSpigot`
project.ext.spigotBuildToolsUrl = 'https://hub.spigotmc.org/jenkins/job/BuildTools/lastSuccessfulBuild/artifact/target/BuildTools.jar'

// Dependency versions
project.ext.bukkitVersion = '1.14.0-R0.1-SNAPSHOT'
project.ext.spigotVersion = '1.14.0-R0.1-SNAPSHOT'
project.ext.vaultApiVersion = '1.7'
project.ext.worldEditCoreVersion = '7.2.6-SNAPSHOT'
project.ext.worldGuardBukkitVersion = '7.0.6-SNAPSHOT'
project.ext.placeholderApiVersion = '2.10.10-DEV-123'
project.ext.jetbrainsAnnotationsVersion = '16.0.2'
project.ext.juintVersion = '5.7.0'
project.ext.latestMcVersion = '1.17'

// Use `gradlew build` just to build the jar file.
// To use these tasks effectively, download Spigot to this folder.
project.ext.testServerDir = new File('./run/')
project.ext.outputDir = new File('./OUT/')

project.ext.readmeIn = './unbuilt_readme.md'
project.ext.readmeOut = './README.md'

// Tokens to replace within files
project.ext.tokens = [
    "PLUGIN_VERSION": project.version,
    "MAIN_CLASS"    : project.ext.mainClass,
    "PLUGIN_NAME"   : project.ext.pluginName,
    "LIVE_VERSION"  : project.ext.liveVersion,
    "SPIGOT_VERSION": project.ext.spigotVersion.substring(0, project.ext.spigotVersion.indexOf('-')),
    "LATEST_MC_VERSION": project.ext.latestMcVersion
]

java {
    // Use Java 8
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}


// -- DEPENDENCIES -- //


// Extra repos for Bukkit/Spigot stuff
repositories {
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://maven.sk89q.com/repo/' }
    maven { url 'https://repo.mikeprimm.com' }
    maven { url 'https://papermc.io/repo/repository/maven-public/' }
    maven { url 'https://repo.extendedclip.com/content/repositories/dev/' }

    // Why do you have to be special, huh?
    maven {
        url = 'http://nexus.hc.to/content/repositories/pub_releases/'
        allowInsecureProtocol = true
    }
}

dependencies {
    // Things needed to compile the plugin
    implementation "org.jetbrains:annotations:$jetbrainsAnnotationsVersion"
    implementation "org.spigotmc:spigot-api:$spigotVersion"
    implementation "net.milkbowl.vault:VaultAPI:$vaultApiVersion"
    implementation "com.sk89q.worldedit:worldedit-core:$worldEditCoreVersion"
    implementation "com.sk89q.worldguard:worldguard-bukkit:$worldGuardBukkitVersion"
    implementation "me.clip:placeholderapi:$placeholderApiVersion"

    testImplementation "org.junit.jupiter:junit-jupiter-api:$juintVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$juintVersion"
}

test {
    useJUnitPlatform()

    systemProperties = [
        'junit.jupiter.conditions.deactivate'           : '*',
        'junit.jupiter.extensions.autodetection.enabled': 'true',
        'junit.jupiter.testinstance.lifecycle.default'  : 'per_class'
    ]
}


// -- BUILDING README -- //


// Fill in readme placeholders
task updateReadme {
    // Set the inputs and outputs for the operation
    inputs.file project.ext.readmeIn
    outputs.file project.ext.readmeOut

    // Copy the new readme
    doLast {
        copy {
            from project.ext.readmeIn
            into file(project.ext.readmeOut).getParent()
            rename { n -> file(project.ext.readmeOut).getName() }
            filter ReplaceTokens, tokens: project.ext.tokens
        }
    }
}

clean {
    // Delete old build(s)
    project.delete(files(project.ext.outputDir))

    // Delete old build(s) from test server plugin dir
    project.delete(fileTree(new File(testServerDir, '/plugins/'))
            .include('ClaimChunk**.jar')
            .include('claimchunk**.jar'))
}

// Replace placeholders with values in source and resource files
processResources {
    filter ReplaceTokens, tokens: project.ext.tokens
}


// -- BUILDING JARS -- //


// Enable all compiler warnings for cleaner (hopefully) code
compileJava {
    options.warnings = true
    options.deprecation = true
    options.compilerArgs += ['-Xlint:all', '-Xmaxwarns', '200']
    options.encoding = 'UTF-8'
}

// Clear out old Spigot versions from test server directory
task deleteOldSpigotInstalls(type: Delete) {
    delete fileTree(testServerDir).include('spigot-*.jar')
}

// Download and run Spigot BuildTools to generate a Spigot server jar in the spigot `testServerDir`
task installSpigot(type: JavaExec) {
    dependsOn deleteOldSpigotInstalls

    main = "-jar"
    workingDir file(testServerDir)
    args file(new File((File) testServerDir, '/BuildTools.jar'))
    doFirst {
        download {
            src spigotBuildToolsUrl
            dest file(new File((File) testServerDir, '/BuildTools.jar'))
            overwrite true
        }
    }
}

// Copy from the libs dir to the plugins directory in the testServerDir
task copyClaimChunkToPluginsDir(type: Copy) {
    dependsOn jar
    from file(jar.outputs.files.singleFile)
    into file(new File(testServerDir, '/plugins/'))
    rename 'claimchunk-(.*).jar', 'claimchunk-$1.jar'
    rename 'ClaimChunk-(.*).jar', 'ClaimChunk-$1.jar'
}

// Copy from the libs dir to the plugins directory in the testServerDir
task copyClaimChunkToOutputDir(type: Copy) {
    dependsOn jar
    from file(jar.outputs.files.singleFile)
    into file(outputDir)
    rename 'claimchunk-(.*).jar', 'claimchunk-$1.jar'
    rename 'ClaimChunk-(.*).jar', 'ClaimChunk-$1.jar'
}

// When the build task is run, copy the version into the testServerDir and output
// (Also rebuild the README because Gradle and IDEA aren't getting along too well)
build.finalizedBy updateReadme
build.finalizedBy copyClaimChunkToPluginsDir
build.finalizedBy copyClaimChunkToOutputDir
